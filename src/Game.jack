// Основной класс игры. Запускает стартовое меню, а затем и саму игру

class Game {
    field Ball ball;
    field Map map;
    field Power powersMenu;
    field HelloMenu helloMenu;
    field StartMenu startMenu;
    field Wall wall;
    field int currentScore, bestScore, lastHundred;
    field boolean isGameStarted;

    //ИНИЦИАЛИЗАЦИЯ НОВОЙ ИГРЫ
    constructor Game new() {
        let currentScore = 0;
        let lastHundred = 0;
        let bestScore = 0;
        let ball = Ball.new();
        let map = Map.new();
        let wall = Wall.new();
        let isGameStarted = false;
        return this;
    }

    //ЗАПУСК ИГРЫ
    method void start() {
        do helloMenu.draw();
        while (true) {
            do startMenu.draw(bestScore);
            do run();
        }
        return;
    }

    //ПРОЦЕСС ИГРЫ
    method void run() {
        var char key;
        var boolean exit;
        var string scoreString, f2String, f3String; // Здесь строки нужны, чтобы избежать переполнения
        
        // Все, что относится к способностям
        var boolean isImmortal, immortalUsed, isSlowed, slowUsed;
        var int immortalCounter;

        let exit = false;
        let isImmortal = false;
        let immortalUsed = false;
        let isSlowed = false;
        let slowUsed = false;
        let immortalCounter = 0;
        let scoreString = "Current score: ";
        let f2String = "F2";
        let f3String = "F3";

        do wall.defaultSpeed();
        
        // Бежим, пока не проиграли или не вышли из игры
        while (~exit) {
        
            let key = Keyboard.keyPressed();
            if (key = 32) {
                do ball.jump();
            }
            if (key = 140) {
                do pauseMenu();
            }
            if (key = 142) {
                // Запускаем бессмертие на 3 удара, если еще не запускали
                if (~immortalUsed) {
                    let isImmortal = true;
                    let immortalUsed = true;
                    let immortalCounter = 0;
                }
            }
            if (key = 143) {
                // Сбрасываем скорость до дефолтной, если еще не сбрасывали
                if (~slowUsed) {
                    let slowUsed = true;
                    let isSlowed = true;
                    do wall.defaultSpeed();
                }
            }
                
            do wall.move();
            do ball.update();
                
            // Проверка столкновения со стеной
            if (wall.checkCollision(ball)) {
                if (~isImmortal) {
                    let isGameStarted = false;
                    do wall.reset();
                    do ball.reset();
                    if (currentScore > bestScore) {
                        let bestScore = currentScore;
                    }
                    let currentScore = 0;
                    return;
                }
                else {
                    // Если режим бессмертия, то считаем + 1 столкновение
                    do wall.reset();
                    let immortalCounter = immortalCounter + 1;
                }
            }

            // Если использовали 3 столкновения в бессмертии, то выключаем его
            if (immortalCounter = 3) {
                let isImmortal = false;
                let immortalCounter = 0;
            }
            
            // Обновляем счетчик. Каждую сотню увеличиваем скорость на 1
            let currentScore = currentScore + 1;
            if (currentScore > (lastHundred + 100)) {
                let lastHundred = lastHundred + 100;
                do wall.increaseSpeed();
            }
            
            // Перерисовываем объекты на экране
            do updateRunElements();

            // Перерисовываем все надписи
            do Output.moveCursor(1, 54);
            do Output.printString(f2String);
            do Output.moveCursor(1, 60);
            do Output.printString(f3String);

            do Output.moveCursor(0, 0);
            do Output.printString(scoreString);
            do Output.printInt(currentScore);

            // Если использовали суперсилу, то ее зачеркиваем
            if (immortalUsed) {
                do Power.crossF2();
            }
            if (slowUsed) {
                do Power.crossF3();
            }
            
            do Sys.wait(25);
        }
        return;
    }

    // ОБНОВЛЕНИЕ ОБЪЕКТОВ НА ЭКРАНЕ
    method void updateRunElements() {
        do Screen.clearScreen();
        do map.draw();
        do ball.draw();
        do wall.draw();
        do Power.draw();
        return;
    }
    
    //МЕНЮ ПАУЗЫ (по ESC)
    method void pauseMenu()
    {
        var boolean continue;
        var Button btn;
        var int key;

        let continue = false;
        
        do Screen.clearScreen();

        do Output.moveCursor(4, 21);
        do Output.printString("Current score is ");
        do Output.printInt(currentScore);

        let btn = Button.new(130, 100, 250, 50, "PRESS ESC TO CONTINUE");
        do btn.print();

        do Sys.wait(100);
        while (~continue) {
            let key = Keyboard.keyPressed();
            if (key = 140) {
                let continue = true;
            }
        }
        do Sys.wait(100);
        return;
    }
}