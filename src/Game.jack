class Game {
    field int currentScore, bestScore;
    field Ball ball;
    field Map map;
    field boolean isGameStarted;
    field Wall wall;

    //ИНИЦИАЛИЗАЦИЯ НОВОЙ ИГРЫ
    constructor Game new() {

        let currentScore = 0;
        let bestScore = 0;
        let ball = Ball.new();
        let map = Map.new();
        let wall = Wall.new();
        let isGameStarted = false;
        return this;
    }

    //ЗАПУСК ИГРЫ
    method void start() {
        while (true) {
            do start_menu();
            do run();
        }
        return;
    }

    //МЕНЮ ПЕРЕД НАЧАЛОМ ИГРЫ
    method void start_menu() {
        var boolean start;
        var Button btn;
        var int key;
        let start = false;
        
        do Screen.clearScreen();

        do Output.moveCursor(4, 23);
        do Output.printString("Best score is ");
        do Output.printInt(bestScore);

        let btn = Button.new(130, 100, 250, 50, "PRESS ENTER TO START");
        do btn.print();

        while (~start) {
            let key = Keyboard.keyPressed();
            if (key = 128) {
                let start = true;
            }
        }
        return;
    }

    //ПРОЦЕСС ИГРЫ
    method void run() { //TODO: переполнение памяти!!!
        var char key;
        var boolean exit;
        var string scoreString;
        let exit = false;
    
        let scoreString = "Current score: ";
        
        while (~exit) {
            let key = Keyboard.keyPressed();

            if (key = 32) {
                do ball.jump();
            }
            if (key = 140) {
                do pause_menu();
            }
                
            //do map.updateMap();
            do wall.move();
            do ball.update();
                
            // если мяч врезался в стену
            if (wall.checkCollision(ball)) {
                let isGameStarted = false;
                do wall.reset();
                do ball.reset();
                if (currentScore > bestScore) {
                    let bestScore = currentScore;
                }
                let currentScore = 0;
                return;
            }
                
            let currentScore = currentScore + 1;
            
            do Screen.clearScreen();

            do map.draw();
            do ball.draw();
            do wall.draw();

            do Output.moveCursor(0, 0);
            do Output.printString(scoreString);
            do Output.printInt(currentScore);
            
            do Sys.wait(25);
        }
        return;
    }

    //МЕНЮ ПАУЗЫ (по ESC)
    method void pause_menu()
    {
        var boolean continue;
        var Button btn;
        var int key;
        let continue = false;
        
        do Screen.clearScreen();

        do Output.moveCursor(4, 21);
        do Output.printString("Current score is ");
        do Output.printInt(currentScore);

        let btn = Button.new(130, 100, 250, 50, "PRESS ESC TO CONTINUE");
        do btn.print();

        do Sys.wait(100);
        while (~continue) {
            let key = Keyboard.keyPressed();
            if (key = 140) {
                let continue = true;
            }
        }
        do Sys.wait(100);
        return;
    }
}